plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.5.14'
    id 'java'
    id "org.owasp.dependencycheck" version "5.3.1"
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/5.5.0'

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/build-docker.gradle"
apply from: "$scriptsUrl/build-info.gradle"
apply from: "$scriptsUrl/build-commons.gradle"
//apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/release-service.gradle"
apply from: "$scriptsUrl/signing.gradle"

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    maven { url 'https://repo.spring.io/libs-milestone' }
    if (releaseMode) {
        dependencyRepos.forEach { path ->
            maven {
                setUrl("https://maven.pkg.github.com/reportportal/${path}")
                credentials {
                    username = findProperty("githubUserName")
                    password = findProperty("githubToken")
                }
            }
        }
    } else {
        maven { url 'https://jitpack.io' }
    }
}

//https://nvd.nist.gov/vuln/detail/CVE-2020-10683 (dom4j 2.1.3 version dependency) AND https://nvd.nist.gov/vuln/detail/CVE-2019-14900
ext['hibernate.version'] = '5.4.18.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-10693
ext['hibernate-validator.version'] = '6.1.5.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-13692
//ext['postgresql.version'] = '42.2.13'
//https://nvd.nist.gov/vuln/detail/CVE-2020-9488
ext['log4j2.version'] = '2.17.1'


dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:f0b6bb6b')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-dao'
        compile 'com.epam.reportportal:commons-rules'
        compile 'com.epam.reportportal:commons-model'
    } else {
        compile 'com.github.reportportal:commons-dao:513abc7'
        compile 'com.github.reportportal:commons-rules:331c402'
        compile 'com.github.reportportal:commons-model:a215b88'
    }

    //Fix CVE-2021-41079, CVE-2022-23181, CVE-2021-33037, CVE-2021-30640, CVE-2022-42252
    compile 'org.apache.tomcat.embed:tomcat-embed-core:9.0.68'
    compile 'org.apache.tomcat.embed:tomcat-embed-el:9.0.68'
    compile 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.68'
    //Fix CVE-2020-15522
    compile 'org.bouncycastle:bcprov-jdk15on:1.69'
    //Fix CVE-2015-7501, CVE-2015-4852
    compile 'commons-collections:commons-collections:3.2.2'
    //Fix CVE-2018-10237
    compile 'com.google.guava:guava:24.1.1-jre'
    //Fix CVE-2020-13956
    compile 'org.apache.httpcomponents:httpclient:4.5.13'
    //Fix CVE-2022-40152
    compile 'com.fasterxml.woodstox:woodstox-core:5.4.0'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-security'

    ///// Security
    //https://nvd.nist.gov/vuln/detail/CVE-2020-5407 AND https://nvd.nist.gov/vuln/detail/CVE-2020-5408
    compile 'org.springframework.security:spring-security-core:5.5.8'
    compile 'org.springframework.security:spring-security-config:5.5.8'
    compile 'org.springframework.security:spring-security-web:5.5.8'
    //

    compile 'org.springframework.security:spring-security-oauth2-client'
    //Fix CVE-2022-22969
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE'
    compile 'org.springframework.security:spring-security-jwt:1.0.11.RELEASE'
    compile 'org.springframework.security:spring-security-ldap'
    compile 'org.springframework.security.extensions:spring-security-saml2-core:2.0.0.M31'
//  Temporary fix of https://nvd.nist.gov/vuln/detail/CVE-2019-12400
    compile 'org.apache.santuario:xmlsec:2.1.7'

    compile 'org.apache.tika:tika-core'

    compile 'javax.inject:javax.inject:1'
    compile 'io.springfox:springfox-swagger2'
    compile 'org.apache.commons:commons-compress:1.21'
    compile 'org.cryptacular:cryptacular:1.1.4'
    compile 'org.yaml:snakeyaml:1.32'
    compile 'org.hibernate:hibernate-core:5.4.24.Final'
    compile 'org.springframework:spring-core:5.3.20'
}

processResources {
    dependsOn(gitInfo)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

bootJar {
    System.getenv().containsKey("K8S") ? getArchiveFileName().set('app.jar') : archiveClassifier.set('exec')
}
jar.enabled(true)
jar.archiveClassifier.set('')

addDockerfileToGit.dependsOn createDockerfile
beforeReleaseBuild.dependsOn addDockerfileToGit
publish.dependsOn build
publish.mustRunAfter build

