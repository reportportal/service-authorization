plugins {
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.springframework.boot' version '2.5.15'
    id 'java'
    id 'java-library'
    id "org.owasp.dependencycheck" version "8.2.1"
}

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/build-docker.gradle"
apply from: "$scriptsUrl/build-info.gradle"
apply from: "$scriptsUrl/build-commons.gradle"
//apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/release-service.gradle"
apply from: "$scriptsUrl/signing.gradle"

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

//https://nvd.nist.gov/vuln/detail/CVE-2020-10683 (dom4j 2.1.3 version dependency) AND https://nvd.nist.gov/vuln/detail/CVE-2019-14900
ext['hibernate.version'] = '5.4.18.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-10693
ext['hibernate-validator.version'] = '6.1.5.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-13692
//ext['postgresql.version'] = '42.2.13'
//https://nvd.nist.gov/vuln/detail/CVE-2020-9488
ext['log4j2.version'] = '2.21.1'


dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + '5.10.0' : 'com.github.reportportal:commons-bom:04b1a7d')
    }
}

dependencies {
    if (releaseMode) {
        api 'com.epam.reportportal:commons-dao'
        api 'com.epam.reportportal:commons-rules'
        api 'com.epam.reportportal:commons-model'
    } else {
        api 'com.github.reportportal:commons-dao:17f6d28343'
        api 'com.github.reportportal:commons-rules:83eda6051be4'
        api 'com.github.reportportal:commons-model:af61954'
    }

    //Fix CVE-2021-41079, CVE-2022-23181, CVE-2021-33037, CVE-2021-30640, CVE-2022-42252
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.82'
    implementation 'org.apache.tomcat.embed:tomcat-embed-el:9.0.82'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.82'
    //Fix CVE-2020-15522
    implementation 'org.bouncycastle:bcprov-jdk15on:1.69'
    //Fix CVE-2015-7501, CVE-2015-4852
    implementation 'org.apache.commons:commons-collections4:4.4'
    //Fix CVE-2018-10237
    implementation 'com.google.guava:guava:31.1-jre'
    //Fix CVE-2020-13956
    api 'org.apache.httpcomponents:httpclient:4.5.14'
    //Fix CVE-2022-40152
    api 'com.fasterxml.woodstox:woodstox-core:6.5.1'

    api 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    ///// Security
    //https://nvd.nist.gov/vuln/detail/CVE-2020-5407 AND https://nvd.nist.gov/vuln/detail/CVE-2020-5408
    implementation 'org.springframework.security:spring-security-core:5.8.5'
    implementation 'org.springframework.security:spring-security-config:5.8.5'
    implementation 'org.springframework.security:spring-security-web:5.8.5'
    //

    api 'org.springframework.security:spring-security-oauth2-client'
    //Fix CVE-2022-22969
    api 'org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE'
    implementation 'org.springframework.security:spring-security-jwt:1.0.11.RELEASE'
    implementation 'org.springframework.security:spring-security-ldap'
    // TODO: consider migration to spring-security-saml2-service-provider
    implementation 'org.springframework.security.extensions:spring-security-saml2-core:2.0.0.M31'
//  Temporary fix of https://nvd.nist.gov/vuln/detail/CVE-2019-12400
    implementation 'org.apache.santuario:xmlsec:3.0.3'

    implementation 'org.apache.tika:tika-core'

    api 'javax.inject:javax.inject:1'
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    api 'org.apache.commons:commons-compress:1.21'
    implementation 'org.cryptacular:cryptacular:1.1.4'
    // TODO: snakeyaml 2.0 supported by Spring Boot 3 only
    implementation 'org.yaml:snakeyaml:1.33'
    implementation 'org.hibernate:hibernate-core:5.4.24.Final'
    implementation 'org.springframework:spring-core:5.3.30'
    implementation "com.rabbitmq:http-client:5.2.0"

    // add lombok support
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

processResources {
    dependsOn(gitInfo)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
}

bootJar {
    System.getenv().containsKey("K8S") ? getArchiveFileName().set('app.jar') : archiveClassifier.set('exec')
}
jar.enabled(true)
jar.archiveClassifier.set('')


publish.dependsOn build
publish.mustRunAfter build
